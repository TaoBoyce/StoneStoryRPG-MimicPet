/*
Bouncing Pet Base
written by Bitty45
version 1.2.0

A base for bouncing pets, to make them super easy to make.
Override EDITABLE VALUES and ART with what you want for your own pet.
Be sure that if you're using this, to use 'new',
    otherwise your pet may conflict with others using this base.
example: var pet = new BouncingPetBase
Also, make sure to call Main() in your script
example: pet.Main()
see MimicPet for a more in depth example of how to use it.
*/

//EDITABLE VALUES=======================
var PET_COLOR = #ffffff
var MAX_DIST = 15 //triggers a jump if X is farther than this
var MAX_DIST_BOSS = 30 //same as MAX_DIST, but against bosses
var MIN_DIST = 3 //how close it will try to jump to the player, can go over
var MAX_JUMP = 5 //how high it will jump
var MAX_Z_DIST = 5 //triggers a jump if Z is farther than this
//how fast it'll move towards targetX, getting slower as it approaches targetX (min of 1)
var MOVE_PERCENT = 0.05

var PRINT_X_OFFSET = 0 //offsets the X of the print
var PRINT_Y_OFFSET = 0 //offsets the Y of the print

/*
The animation system is based on how far in the jump the pet is, in percent
the default holds the jump art from 0%-40%, the peak art from 40%-60%,
    the fall art from 60%-100%.
*/
//start of jump animation, dictates how long it'll hold the jump animation
//goes from 0% to JUMP_ANIM_PERCENT
var JUMP_ANIM_PERCENT = 0.4
//start of peak animation, dictates how long it'll hold the peak animation
//goes from JUMP_ANIM_PERCENT to PEAK_ANIM_PERCENT
var PEAK_ANIM_PERCENT = 0.6
//fall animation is whatever percent is left over
//goes from PEAK_ANIM_PERCENT to 100%

//ART===================================
//Keep art the same size, otherwise it may look weird

//state SIT
var petSit = ui.AddAnim(ascii
#
#
asciiend)

//state JUMP
var petJump = ui.AddAnim(ascii
#
#
asciiend)

//state PEAK
var petPeak = ui.AddAnim(ascii
#
#
asciiend)

//state FALL
var petFall = ui.AddAnim(ascii
#
#
asciiend)


//MAIN CODE=============================
var petJumping = false
var playerX = pos.x 
var playerY = pos.y
var playerZ = pos.z
var petX = playerX - MIN_DIST
var petY = playerY
var petZ = playerZ
var petPrevX
var petPrevY
var petPrevZ
var targetX
var targetY
var targetZ
var petPnl
var totTime
var notRocky = (loc ! rocky)

func initBouncePet()
  var addAndInitAnim = func(anim, y, dur, loop)
    petPnl.Add(anim)
    anim.anchor = top_left
    anim.dock = top_left
    anim.x = 0
    anim.y = y
    anim.duration = dur
    anim.loop = loop
    anim.color = PET_COLOR
    anim.Play()
  
  petPnl = ui.AddPanel()
  petPnl.h = 1
  petPnl.w = 1
  petPnl.style = 0
  petPnl.anchor = top_left
  petPnl.dock = top_left
  addAndInitAnim(petSit, 0, 40, true)
  addAndInitAnim(petJump, 0, 40, true)
  addAndInitAnim(petPeak, 0, 40, true)
  addAndInitAnim(petFall, 0, 40, true)
  PetDefaultState()
  
func Main()
  //update
  totTime = totalTime
  playerX = pos.x 
  playerY = pos.y
  playerZ = pos.z
  
  //reset
  ?!petPnl
    initBouncePet()
    
  ?totTime = 1 | time = 1 & notRocky
    PetDefaultState()
    petX = playerX - MIN_DIST
    petY = playerY
    petZ = playerZ
      
  //main loop
  ?petJumping
    //currently jumping
    petZ += getZChange()
    petX += getXChange()
    ?targetX - petPrevX > 0 //only jumps if target is further than 0
      petY = getJumpYArch()
    setAnimation()
    ?targetX - petX < 1
      PetDefaultState()
  : 
    //start jump
    ?!nearPlayer()
      petJumping = true
      petPrevX = petX
      petPrevY = petY
      petPrevZ = petZ
      targetX = playerX - MIN_DIST
      targetY = playerY
      targetZ = playerZ
      petJump.visible = true
      petSit.visible = false
  
  PrintPet()

func nearPlayer()
  ?foe = boss
    ?foe = bronze_guardian
      return (playerX - petX < MAX_DIST_BOSS) | (foe.distance < 25)
    :
      return playerX - petX < MAX_DIST_BOSS
  :
    return playerX - petX < MAX_DIST & math.Abs(playerZ - petZ) < MAX_Z_DIST

func setAnimation()
  var percent = getPercentToTarget()
  ?percent <= JUMP_ANIM_PERCENT
    petSit.visible = false
    petJump.visible = true
  :?percent <= PEAK_ANIM_PERCENT
    petJump.visible = false
    petPeak.visible = true
  :
    petPeak.visible = false
    petFall.visible = true

func PetDefaultState()
  petJumping = false
  petSit.visible = true
  petJump.visible = false
  petPeak.visible = false
  petFall.visible = false
  petPrevX = petX
  petPrevY = petY
  petPrevZ = petZ   
  targetX = playerX - MIN_DIST
  targetY = playerY
  targetZ = playerZ

func getPercentToTarget()
  var part = petX - petPrevX
  var whole = targetX - petPrevX
  var percent = (part*1.0)/(whole*1.0)
  return percent

func getJumpYArch()
  //creates a parabola between start and end points, with max height
  //a = -jumpheight / (( mid - start )( mid - end ))
  //(y(x) = ( x - start )( x - end )a
  var MidMinusStart = getMidpoint()-petPrevX
  var MidMinusEnd = getMidpoint()-targetX
  var multi = (-MAX_JUMP)/(MidMinusStart*MidMinusEnd)
  var XMinusStart = petX-petPrevX
  var XMinusEnd = petX-targetX
  var result = XMinusStart*XMinusEnd*multi
  return math.RoundToInt(result)
  
func getMidpoint()
  return petPrevX + (targetX - petPrevX)/2.0

func getXChange()
  //how many characters pet will move per frame
  //start distance * MOVE_PERCENT, rounded, minimum 1
  return math.Max(1,math.RoundToInt((targetX-petX)*MOVE_PERCENT))
  
func getZChange()
  //moves pet towards or away from foreground
  ?petZ < targetZ
    return 1
  :?petZ > targetZ
    return -1
  :
    return 0
    
func PrintPet()
  var printX
  var printY
  printX = screen.FromWorldX(petX + PRINT_X_OFFSET)
  printY = screen.FromWorldZ(petY + petZ + PRINT_Y_OFFSET)
  petPnl.x = printX
  petPnl.y = printY
  
  /*
  //test printing
  >`0,4,pet print coord: @printX@,@printY@
  >`0,5,panel: @petPnl.x@,@petPnl.y@
  >`0,6,player screen coord: @screen.FromWorldX(playerX)@,@screen.FromWorldZ(playerZ-playerY)@
  >`0,7,player @playerX@,@playerY@,@playerZ@
  >`0,8,pet coord: @petX@,@petY@,@petZ@
  >`0,9,pet prev coord: @petPrevX@,@petPrevY@,@petPrevZ@
  >`0,10,target coord: @targetX@,@targetY@,@targetZ@
  >`0,11,percent to target: @getPercentToTarget@
  */